plugins {
    id 'java-library'
    id 'application'
    id 'maven-publish'
    id 'signing'
}
group = 'jp.simplespace'
version = '1.0-SNAPSHOT'
dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:22.0.0'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'MatchanPluginUtilities'
            from components.java
            pom {
                name = 'MatchanPluginUtilities'
                description = 'Spigotプラグイン開発を楽にするためのライブラリ'
                url = 'https://github.com/Matchan0727/MatchanPluginUtilities'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'matchan'
                        name = 'Matchan'
                        email = 'matchan0727ahaha@gmail.com'
                    }
                }
                scm {
                    connection = 'git@github.com:Matchan0727/MatchanPluginUtilities.git'
                    developerConnection = 'git@github.com:Matchan0727/MatchanPluginUtilities.git'
                    url = 'https://github.com/Matchan0727/MatchanPluginUtilities'
                }
            }
        }
    }
    repositories{
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/content/repositories/releases/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            allowInsecureProtocol=true
            credentials {
                username = "${sonatypeUsername}"
                password = "${sonatypePassword}"
            }
        }
    }
}
signing {
    sign publishing.publications.mavenJava
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}